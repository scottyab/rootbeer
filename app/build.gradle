apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


android {
    namespace = "com.scottyab.rootbeer.sample"
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId "com.scottyab.rootbeer.sample"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 10
        versionName "0.9"
        vectorDrawables.useSupportLibrary = true

        archivesBaseName = "RootBeerSample-$versionName-[$versionCode]"

    }
    buildFeatures {
        viewBinding = true
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
        signingConfigs {
            //from ~/user/.gradle/gradle.properties
            release {
                storeFile file(ROOTBEER_SAMPLE_STORE)
                keyAlias ROOTBEER_SAMPLE_KEY
                storePassword ROOTBEER_SAMPLE_STORE_PASS
                keyPassword ROOTBEER_SAMPLE_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    java {
        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
}

dependencies {
    implementation project(':rootbeerlib')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "androidx.core:core-ktx:$corektxVersion"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "com.google.android.material:material:$materialDesignVersion"

    implementation "com.nineoldandroids:library:$nineoldandroidsVersion"
    implementation "uk.co.barbuzz:beerprogressview:$beerProgressViewVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"
}
